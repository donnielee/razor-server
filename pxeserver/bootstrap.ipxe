#!ipxe

# The number of tries made to obtain DHCP configuration to date, and the
# maximum number to make before we give up.  Each time DHCP failed we will
# sleep one secord per attempt already made, starting at one second, and
# ramping up as we go.
set tries:uint32    0
set maxtries:uint32 60

:retry

isset ${ip} || goto dhcp_net0
set dhcp_mac ${mac:hexhyp}
goto chain_boot

:dhcp_net0
isset ${net0/mac} && dhcp net0 || goto dhcp_net1
echo net0 has DHCP
set dhcp_mac net0$${net0/mac:hexhyp}

:dhcp_net1
isset ${net1/mac} && dhcp net1 || goto dhcp_net2
echo net1 has DHCP
set dhcp_mac net1$${net1/mac:hexhyp}

:dhcp_net2
isset ${net2/mac} && dhcp net2 || goto chain_boot
echo net2 has DHCP
set dhcp_mac net2$${net2/mac:hexhyp}


:chain_boot
chain http://192.168.41.137:8080/svc/boot?net0=${net0/mac:hexhyp}&net1=${net1/mac:hexhyp}&net2=${net2/mac:hexhyp}&dhcp_mac=${dhcp_mac}&serial=${serial}&asset=${asset}&uuid=${uuid} || goto error
echo successfully completed the chain-loaded script; booting locally with 'exit'
exit

:error
iseq ${tries} ${maxtries} && goto failed
inc tries
sleep ${tries}
goto retry

:failed
echo failed to obtain DHCP data after ${tries} attempts, giving up.
sleep 60
reboot

